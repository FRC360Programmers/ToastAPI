/**
Template file for classes using GradleRIO
@author Jaci
*/

buildscript {
  repositories {
	mavenCentral()
	maven {
		name = "GradleRIO"
		url = "http://dev.sourcecoded.info/jaci/maven"
	}
  }
  dependencies {
    classpath group: 'jaci.openrio.gradle', name: 'GradleRIO', version: '1.1.3'			//Change this line if you wish to Update GradleRIO
  }
}

apply plugin: 'GradleRIO'                                 //Apply the GradleRIO plugin
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: BuildGradle
apply plugin: 'maven'

gradlerio.robotClass = "jaci.openrio.toast.core.Toast"    //The class for the main Robot Class. Used in manifest
gradlerio.team = "5333"                                   //Your FRC team number (e.g. 5333 for team 'Can't C#', or 47 for Chief Delphi)
//gradlerio.rioIP = "10.53.33.20"                         //Uncomment to specify the IP address of the RIO

ext.mavenProps = file "../maven.properties"
mavenProps.withReader {
  def prop = new Properties()
  prop.load(it)
  project.ext.mavenProps = new ConfigSlurper().parse prop
}

configurations {
    deployerJars
}

repositories {
    mavenCentral()
}

dependencies {
  deployerJars 'org.apache.maven.wagon:wagon-ssh:2.2'
}

archivesBaseName = "Toast"
version = "0.0.1-R5-DEV"
group = "jaci.openrio.toast"

def robotManifest = {
  //attributes 'Main-Class': 'edu.wpi.first.wpilibj.RobotBase'
  attributes 'Main-Class': 'jaci.openrio.toast.core.ToastBootstrap'
  attributes 'Robot-Class': gradlerio.robotClass
}

jar.doFirst {
  from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
  manifest robotManifest
}

task genJavadoc(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

task genPatches << {
  ant.delete(dir: "build/patches/cache")
  ant.copy(todir: "build/patches/cache") {
    ant.fileset(dir: "patches")
  }
}

task movePatches << {
  ant.copy(todir: "src/main/java/toastpatches") {
    ant.fileset(dir: "build/patches/cache")
  }
}

task configurePatches << {
  String pdir = "build/resources/main/assets/toast/patches";
   ant.move(todir: "${pdir}") {
     ant.fileset(dir: "build/classes/main/") {
       ant.exclude(name: "**/jaci/**")
     }
   }
   ant.move(todir: "${pdir}") {
     ant.fileset(dir: "${pdir}")
     ant.mapper(type: "glob", from: "*.class", to: "*.sim")
   }

  //  def content = ""
  //  def file = new File("${pdir}/patches.txt")
  //  def tree = fileTree(dir: "${pdir}", include: "**/*.sim")
   //
  //  tree.visit {
  //    fileDetails -> content += "${fileDetails.relativePath}" + "\n"
  //  }
   //
  //  file.write content
}

task deletePatches << {
  ant.delete(dir: "src/main/java/toastpatches")
}

task updateLatest << {
  ant.delete(dir: "build/latest/")
  ant.copy(file: "${jar.archivePath}", todir: "build/latest")
}

compileJava.dependsOn genPatches
compileJava.dependsOn movePatches
compileJava.finalizedBy configurePatches
compileJava.finalizedBy deletePatches
build.finalizedBy updateLatest

task src(type: Jar) {
  classifier = 'src'
  from sourceSets.main.allSource
}

artifacts {
  //archives genJavadoc
}
uploadArchives {
  repositories.mavenDeployer {
    configuration = configurations.deployerJars
    repository(url: mavenProps.jaci.url) {
      authentication(userName: mavenProps.jaci.user, password: mavenProps.jaci.auth)
    }

    pom {
      groupId = project.group
            version = project.version
            artifactId = project.archivesBaseName
            project {
                name project.archivesBaseName
                packaging 'jar'
                description 'Toast API Module Loader '
            }
    }
  }
}

class BuildGradle implements Plugin<Project> {
  void apply(Project project) {
    project.task('release', dependsOn: 'build') << {
      ant.zip(destfile: "releases/Toast-${project.version}.zip",
        basedir: "release")
    }
  }
}
